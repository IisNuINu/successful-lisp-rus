<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN" "http://www.w3.org/TR/html4/strict.dtd">
<html><head>
<META http-equiv="Content-Type" content="text/html; charset=utf-8">
<title>Successful Lisp - Chapter 13</title>
</head> 

<body bgcolor="white" text="black">
<h1>Глава 13 - Еще больше вещей, которые вы можете
сделать с последовательностями(Sequences)</h1> 

<p>
В этой главе мы познакомимся с наиболее полезными функциями работы с последовательностями и посмотрим, как их использовать. Мы также повторим 
более ранние предостережения о правильном использовании разрушающих
функций.

<h2>CONCATENATE: новая последовательность из старых</h2>

<p><code>CONCATENATE</code>  всегда создает новую последовательность 
из (Конечно же) соединяемых нуля или более последовательностей аргументов. 
Вы должны указать тип результата, а типы аргументов должны быть правильными подтипами типа последовательности.

<pre>
? (concatenate 'list) <i>; no argument sequences</i> 
NIL
? (concatenate 'vector) <i>; no argument sequences</i> 
#()
? (concatenate 'list '(1 2 3) '(4 5))
(1 2 3 4 5)
? (concatenate 'vector #(1 2 3) #(4 5))
#(1 2 3 4 5)
? (concatenate 'list #(1 2 4) '(4 5))
(1 2 3 4 5)
? (concatenate 'vector '(1 2 3) #(4 5))
#(1 2 3 4 5)
? (concatenate 'list "hello") <i>; string is a subtype of sequence</i> 
(#\h #\e #\l #\l #\o)
</pre> 

<h2>ELT и SUBSEQ получают то что вы хотите из любой последовательности (также см., COPY-SEQ)</h2>

<p>
Если вам нужно выбрать один элемент (или диапазон элементов) из
последовательности, вы можете использовать <code>ELT</code> 
(чтобы выбрать один элемент) или <code>SUBSEQ</code> 
(чтобы выбрать диапазон элементов). Но не используйте их, если 
вы действительно не уверены, что не можете сузить тип 
последовательности до вектора или списка; существуют более 
специфические (следовательно, более эффективные) методы доступа 
для менее общих типов.

<p>
<code>SUBSEQ</code> создает копию указанной части последовательности. 
<code>COPY-SEQ</code> тесно связан с <code>SUBSEQ</code>, за исключением 
того, что он копирует все элементы последовательности.

<pre>
? (elt '(1 2 3 4 5) 1) <i>; zero-based indexing</i> 
2
? (subseq '(1 2 3 4 5) 2) <i>; 3rd element through end</i> 
(3 4 5)
? (let ((l '(1 2 3 4 5)))
    (subseq l 2 (length l))) <i>; same effect as previous</i> 
? (subseq '(1 2 3 4 5) 0 3) <i>; element at ending index is not copied</i> 
(1 2 3)
? (subseq #(#\a #\b #\c #\d #\e) 2 4)
#(#\c #\d)
? (copy-seq '(a b c))
(A B C)
</pre> 

<h2>REVERSE переворачивает последовательность с начала в конец (см. также, NREVERSE)</h2>

<p><code>REVERSE</code>  делает копию последовательности, с обратным 
порядком следования элементов. <code>NREVERSE</code> - это разрушающий
аналог <code>REVERSE</code>; он более эффективен, но он изменяет свой 
входной аргумент.

<p><code>REVERSE</code> обычно используется в коде, аналогичном следующему.

<pre>
(defun collect-even-numbers (number-list)
  (let ((result ()))
    (dolist (number number-list)
      (when (evenp number)
        (push number result)))
    (nreverse result)))
</pre> 

<p>
<code>DOLIST</code> и <code>PUSH</code> собирают четные числа 
в списке результатов, но они находятся в обратном порядке от 
их исходных позиций в списке ввода. Последний <code>NREVERSE</code>
возвращает их к первоначальному порядку. Это безопасное использование
разрушающей функции <code>NREVERSE</code>, потому что переменная 
<code>RESULT</code> не может быть разделена; она забывается, как только 
управление покидает форму  <code>LET</code>.

<h2>LENGTH: в конце концов, размер имеет значение</h2>

<p>
О <code>LENGTH</code> говорить особо нечего. Просто помните, что для списков
<code>LENGTH</code> подсчитывает только элементы списка верхнего уровня, 
а не элементы каких-либо вложенных списков..

<pre>
? (length '((1 2 3) (4 5) (6) 7 () 8 9))
7
</pre> 

<h2>COUNT: когда важно, что находится внутри</h2>

<p>
Если вы обнаружите, что ваша программа фильтрует последовательность 
только для получения длины результата, используйте вместо этого 
<code>COUNT</code> (и связанные с ним функции 
<code>COUNT-IF</code> и <code>COUNT-IF-NOT</code>).

<pre>
? (count 3 '(1 3 3 4 2 5 9 8 3 1 9)) <i>; count occurrences</i> 
3
? (count-if #'oddp '(1 3 3 4 2 5 9 8 3 1 9)) <i>; count matches to predicate</i> 
8
? (count-if-not #'evenp '(1 3 3 4 2 5 9 8 3 1 9)) <i>; count mismatches using predicate</i> 
8
</pre> 

<p>Эти функции принимают аргументы ключевые слова:

<pre>
Keyword      Value                                        Default
-------      -----                                        -------
:START       starting index (inclusive)                   0
:END         ending index (exclusive)                     NIL
:FROM-END    non-NIL to work backwards from end element   NIL
:KEY         function to select match data from element   NIL
</pre> 

<p><code>NIL</code> значение для ключевого слова <code>:END</code> 
обозначает позицию сразу после конца последовательности; поскольку это
исключительный предел, последний элемент будет обработан. (Если вы указали 
индекс последнего элемента, то последний элемент не будет обработан.)

<p>
Ключевое слово <code>:FROM-END</code> полезно в том случае, 
если тестовая функция имеет побочные эффекты, и порядок 
побочных эффектов важен.

<p>
Когда аргумент <code>:KEY</code>  не равен <code>NIL</code>, он должен 
быть функцией одного аргумента, которая извлекает данные из элемента
последовательности. Например:

<pre>
? (count 3 '((1 2 3) (2 3 1) (3 1 2) (2 1 3) (1 3 2) (3 2 1)) :key #'second)
2
</pre> 

<p>
<code>COUNT</code> принимает дополнительные аргументы ключевые слова 
<code>:TEST</code> и <code>:TEST-NOT</code>. Они дают вам компактный 
способ написать тест, который включает в себя второе значение. Сравните 
следующие эквивалентные формы:

<pre>
<i>; Using COUNT-IF and LAMBDA</i> 
(count-if #'(lambda (n) (< 3 n)) '(1 2 3 4 5 6 7))

<i>; Using COUNT and :TEST</i> 
(count 3 '(1 2 3 4 5 6 7) :test #'<)
</pre> 

<p>
Ключевые слова аргументы для предикатов сравнения также позволяют 
определить точное значение равенства. Предикат по умолчанию - <code>EQL</code>,
что верно для идентичных чисел и символов. Дополнительную информацию о 
предикатах сравнения см. В <a href="chapter17.html">Главе 17</a>

<h2>REMOVE, SUBSTITUTE, и другие модификаторы последовательностей</h2>

<p><code>REMOVE</code> удаляет все вхождения указанного элемента из
последовательности

<pre>
? (remove 7 '(1 2 3 a b c t nil 7 0 7 7))
(1 2 3 A B C T NIL 0)
</pre> 

<p>
Аргументы ключевые слова обрабатываются так же, как и для <code>COUNT</code>.
<code>REMOVE-IF</code> и <code>REMOVE-IF-NOT</code>  доступны; их ключевые аргументы обрабатываются таким же образом, как для  <code>COUNT-IF</code>
и <code>COUNT-IF-NOT</code>.

<p><code>:COUNT</code>  аргумент ключевое слово позволяет ограничить 
количество удаляемых одинаковых элементов.


<p><code>SUBSTITUTE</code>  заменяет все вхождения указанного элемента в
 последовательности на другое значение.

<pre>
? (substitute '(q) 7 '(1 2 3 a b c t nil 7 0 7 7))
(1 2 3 A B C T NIL (Q) 0 (Q) (Q))
</pre> 

<p>
Аргументы ключевые слова обрабатываются так же, как и для <code>COUNT</code>. <code>SUBSTITUTE-IF</code> и <code>SUBSTITUTE-IF-NOT</code> доступны; их 
ключевые аргументы обрабатываются таким же образом, как
 <code>COUNT-IF</code> и <code>COUNT-IF-NOT</code>.

<p>A
аргумент ключевое слово <code>:COUNT</code> позволяет ограничить количество
совпадающих элементов подлежащих замене.


<p><code>REMOVE-DUPLICATES</code>  возвращает копию последовательности,
измененную таким образом, чтобы каждый элемент был уникальным.

<pre>
? (remove-duplicates '(1 2 3 a b c (1 2 3) f c g c h b i a j b a k a))
(1 2 3 (1 2 3) F G C H I J B K A)
</pre> 

<p>
Последняя копия каждого идентичного элемента сохраняется в результате, 
если не указан аргумент ключевое слово:  <code>:FROM-END T</code>, который приводит к сохранению первой копии каждого идентичного элемента.

<p>
<code>REMOVE-DUPLICATES</code> также принимает те же аргументы 
ключевые слова, что и <code>COUNT</code>. Аргументы ключевые слова
<code>:TEST</code> и <code>:TEST-NOT</code> позволяют указать предикат 
сравнения, используемый для определения идентичности элементов. Предикат 
по умолчанию - <code>EQL</code>, что верно для идентичных чисел и символов.
Дополнительную информацию о предикатах сравнения см. В 
<a href="chapter17.html">Главе 17</a>

<h2>DELETE, REMOVE-DUPLICATES, DELETE-DUPLICATES, и NSUBSTITUTE.</h2>

<p>
Многие функции в предшествующем разделе имеют разрушающие аналоги. 
Результат разрушающих функций идентичен, но входная последовательность 
может быть разрушена и изменена.

<pre>
Nondestructive    Destructive
--------------    -----------
REMOVE            DELETE
REMOVE-IF         DELETE-IF
REMOVE-IF-NOT     DELETE-IF-NOT
SUBSTITUTE        NSUBSTITUTE
SUBSTITUTE-IF     NSUBSTITUTE-IF
SUBSTITUTE-IF-NOT NSUBSTITUTE-IF-NOT
REMOVE-DUPLICATES DELETE-DUPLICATES
</pre> 

<blockquote>
Помните, что вы <em>не должны</em> зависеть от модификации входных
последовательностей. Единственный результат, гарантирующий правильность, - 
это возвращаемое значение функции.
</blockquote> 

<h2>FILL и REPLACE</h2> 

<p><code>FILL</code> разурушающе изменяет последовательность, заменяя 
каждый элемент новым значением. Он принимает аргументы ключевые слова
для позиций <code>:START</code> и <code>:END</code>; они имеют то же 
значение, что и описанные ранее в этой главе. Измененная 
последовательность возвращается в виде значения <code>FILL</code>.

<pre>
? (fill (list 1 1 2 3 5 8) 7)
(7 7 7 7 7 7)
? (fill (list 1 1 2 3 5 8) '(a b))
((A B) (A B) (A B) (A B) (A B) (A B))
? (fill (list 1 1 2 3 5 8) 7 :start 2 :end 4)
(1 1 7 7 5 8)
</pre> 

<p><code>REPLACE</code> копирует элементы из одной последовательности 
в другую, разрушительно изменяя целевую последовательность. Вы можете 
указать диапазон элементов для использования в обеих последовательностях; 
более короткий из двух диапазонов определяет количество элементов, которые
фактически копируются.

<pre>
? (let ((a (list 1 2 3 4 5 6 7))
        (b (list 9 8 7 6 5 4 3)))
    (replace a b))

(9 8 7 6 5 4 3)
? (let ((a (list 1 2 3 4 5 6 7))
        (b (list 9 8 7 6 5 4 3)))
    (replace a b :start1 2))

(1 2 9 8 7 6 5)
? (let ((a (list 1 2 3 4 5 6 7))
        (b (list 9 8 7 6 5 4 3)))
    (replace a b :start1 2 :end1 5))
(1 2 9 8 7 6 7)
? (let ((a (list 1 2 3 4 5 6 7))
        (b (list 9 8 7 6 5 4 3)))
    (replace a b :start1 2 :end1 5 :start2 3))
(1 2 6 5 4 6 7)
? (let ((a (list 1 2 3 4 5 6 7))
        (b (list 9 8 7 6 5 4 3)))
    (replace a b :start1 2 :end1 5 :start2 3 :end2 4))
(1 2 6 4 5 6 7)
</pre> 

<h2>Поиск элементов в последовательностях: POSITION, FIND, SEARCH, и 
MISMATCH</h2>

<p>
<code>POSITION</code>  выполняет поиск в последовательности совпадающего 
элемента и возвращает индекс первого совпадения или <code>NIL</code>, 
если в последовательности нет совпадающего элемента.

<pre>
? (position #\a "This is all about you, isn't it?")
8
? (position #\! "This is all about you, isn't it?")
NIL
</pre> 

<code>POSITION</code>  принимает те же ключевые аргументы, что и 
<code>COUNT</code> (описанной ранее в этой главе), и имеет 
(уже знакомые) варианты <code>POSITION-IF</code> и
<code>POSITION-IF-NOT</code>.

<p><code>FIND</code>  аналогичен <code>POSITION</code>, за исключением 
того, что возвращает соответствующий элемент, а не его индекс в
последовательности, если есть совпадение. Как и в <code>POSITION</code>,
вы найдете привычные аргументы  (<code>:FROM-END</code>,
<code>:START</code>, <code>:END</code>, <code>:KEY</code> -- 
и для "базовых" функций <code>:TEST</code> и <code>:TEST-NOT</code>)
и функций вариантов (т.е. <code>FIND-IF</code> и
<code>FIND-IF-NOT</code>).

<pre>
? (find #\a "This is all about you, isn't it?")
#\a
? (find #\! "This is all about you, isn't it?")
NIL
</pre> 

<p><code>SEARCH</code>  возвращает начальную позицию одной 
последовательности в другой последовательности или <code>NIL</code>, 
если совпадение не найдено

<pre>
? (search "ab" "This is all about you, isn't it?")
12
? (search "not so" "This is all about you, isn't it?")
NIL
</pre> 

<p><code>SEARCH</code> принимает аргументы ключевые слова 
<code>:FROM-END</code>, <code>:KEY</code>,
<code>:TEST</code> и <code>:TEST-NOT</code> интерпретирующиеся
как обычно. Вы можете указать диапазон в подстроке (первый
аргумент) используя ключевые слова <code>:START1</code> и 
<code>:END1</code>, и в целевой строке, с помощью
ключевых слов  <code>:START2</code> и <code>:END2</code>.

<p><code>MISMATCH</code> это функциональное дополнение к 
<code>SEARCH</code>, которое возвращает первую позицию, 
в которой подстрока <em>не соответствует</em> части целевой 
строки.

<pre>
? (mismatch "banana" "bananananono")
6
? (mismatch "." "...hello")
1
? (mismatch "............." "...hello")
3
</pre> 

<h2>SORT и MERGE завершают набор инструментов работающих 
с последовательностями</h2>

<p><code>SORT</code>  разрушающе сортирует последовательность; 
порядок определяется предикатом, который вы предоставляете.

<pre>
? (sort (list 9 3 5 4 8 7 1 2 0 6) #'&gt;)
(9 8 7 6 5 4 3 2 1 0)
? (sort (list 9 3 5 4 8 7 1 2 0 6) #'&lt;)
(0 1 2 3 4 5 6 7 8 9)
</pre> 

<p>Входная последовательность разрушающе модифицирована - 
вы должны использовать результат функции.
 

<p><code>STABLE-SORT</code>  сохраняет исходный порядок идентичных элементов; 
<code>SORT</code> не может этого сделать.

<p>Вы можете сортировать структурированные элементы (например, списки, структуры)
с помощью аргумента ключевого слова <code>:KEY</code>, чтобы указать функцию 
извлечения ключа.

<p><code>MERGE</code>  объединяет две входные последовательности 
в один результат. Элементы чередуются в соответствии с предикатом.

Любая входная последовательность может быть разрушающе изменена. 
Вы должны указать тип результата.

<pre>
? (merge 'vector (list 1 3 5 9 8) (vector 2 6 4 7 0) #'&gt;)
#(2 6 4 7 1 3 5 9 8 0)
? (merge 'list (list 1 3 5 9 8) (vector 2 6 4 7 0) #'&lt;)
(1 2 3 5 6 4 7 0 9 8)
? (merge 'vector (list 1 3 5 8 9) (vector 0 2 4 6 7) #'&gt;)
#(1 3 5 8 9 0 2 4 6 7)
? (merge 'list (list 1 3 5 8 9) (vector 0 2 4 6 7) #'&lt;)
(0 1 2 3 4 5 6 7 8 9)
</pre> 

<p>Обратите внимание , что - в общем случае - <code>MERGE</code>
не сортирует цепочку своих аргументов. Предикат используется для 
выбора из одной или другой входной последовательности; ввод из 
выбранной последовательности продолжается до тех пор, пока смысл 
предиката не изменится. Посмотрите на примеры, пока вы не поймете это.

<p><code>MERGE</code> принимает аргумент ключевое слово <code>:KEY</code> 
имеющий привычное значение.

<hr>
<div align="center">
<a href="contents.html">Contents</a> | <a href="cover.html">Cover</a> <br> 
<a href="chapter12.html">Chapter 12</a> | Chapter 13 | <a href="chapter14.html">Chapter 14</a> 
</div>
<hr>
<address>
Copyright &copy; 1995-2001, David B. Lamkins<br>
All Rights Reserved Worldwide<br>
<br>
This book may not be reproduced without the written consent of its author.  Online distribution is restricted to the author's site.
</address> 
</body> </html>